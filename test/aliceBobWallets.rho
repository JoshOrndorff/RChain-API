/**
 * aliceBobWallets - simulate genesis where Alice has 100 REV.
 *
 * We assume the node is started with --has-faucet.
 * This is something of a KLUDGE. The right way would be to
 * put Alice's balance in wallets.txt.
 */
new
  return,
  makeFaucetWallet
in {
  new walletCh in {
    makeFaucetWallet!(100,
      // Alice's public key
      "ed25519", "288755c48c3951f89c5f0ffe885088dc0970fd935bc12adfdd81f81bb63d6219",
      *walletCh) |
    //
    for (@[aliceWallet] <- walletCh) {
      // trace and exports are pre-declared; see loader.rho
      trace!({"aliceWallet": aliceWallet}) |
      exports!(aliceWallet)
    }
  } |

  contract makeFaucetWallet(@amount, @algorithm, @pk, return) = {
    new sysCh, faucetCh, lookup(`rho:registry:lookup`) in {
    // This URI comes from SystemInstancesRegistry.rho.
    lookup!(`rho:id:wdwc36f4ixa6xacck3ddepmgueum7zueuczgthcqp6771kdu8jogm8`, *sysCh) |
      for(@(_, sys) <- sysCh) {
        // This lookup requires that the node is started with --has-faucet
        @sys!("lookup", "faucet", *faucetCh) |
        for(faucet <- faucetCh) {
          trace!({"faucet": *faucet}) |
          faucet!(amount, algorithm, pk, *return)
        }
      }
    }
  }
}
