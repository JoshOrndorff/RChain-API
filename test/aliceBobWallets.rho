new
  ret,
  lookup(`rho:registry:lookup`),
  insertArbitrary(`rho:registry:insertArbitrary`),
  debug(`rho:io:stderr`),
  sysCh, revCh, faucetCh,
  aliceCh, uriCh
in {
  // This URI comes from SystemInstancesRegistry.rho.
  lookup!(`rho:id:wdwc36f4ixa6xacck3ddepmgueum7zueuczgthcqp6771kdu8jogm8`, *sysCh) |
  for(@(_, sys) <- sysCh) {
    @sys!("lookup", "rev", *revCh) |
    @sys!("lookup", "faucet", *faucetCh) |
    for(faucet <- faucetCh; rev <- revCh) {
      debug!({"faucet": *faucet, "rev": *rev}) |
      // We should use WalletCheck... but I'm not sure how to do that with the faucet.
      // I guess I'd have to use my node's wallets.txt.
      faucet!(
        100, "ed25519",
        // Alice's public key
        "288755c48c3951f89c5f0ffe885088dc0970fd935bc12adfdd81f81bb63d6219",
        *aliceCh) |
      for (@[aliceWallet] <- aliceCh) {
        debug!({"aliceWallet": aliceWallet}) |
        insertArbitrary!(aliceWallet, *uriCh) |
        for (@uri <- uriCh) {
          debug!({"uri": uri}) |
          ret!(uri)
        }
      }
    }
  }
}
